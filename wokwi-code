//Autor: Fábio Henrique Cabrini
//Resumo: Esse programa possibilita ligar e desligar o led onboard, além de mandar o status para o Broker MQTT e enviar informações de luminosidade, temperatura e umidade.
//Revisões:
#include <WiFi.h>
#include <PubSubClient.h>
#include <DHT.h>

// Definições do sensor DHT22
#define DHTPIN 15    // Pino onde o DHT22 está conectado
#define DHTTYPE DHT22 // Define o tipo de sensor DHT

DHT dht(DHTPIN, DHTTYPE);  // Instancia o sensor DHT

// Configurações - variáveis editáveis
const char* SSID = "Wokwi-GUEST"; // Nome da rede Wi-Fi
const char* PASSWORD = ""; // Senha da rede Wi-Fi
const char* BROKER_MQTT = "20.197.242.29"; // IP do Broker MQTT
const int BROKER_PORT = 1883; // Porta do Broker MQTT
const char* TOPICO_SUBSCRIBE = "/TEF/lamp001/cmd"; // Tópico MQTT de escuta
const char* TOPICO_PUBLISH_1 = "/TEF/lamp001/attrs"; // Tópico MQTT de envio de status (on/off)
const char* TOPICO_PUBLISH_2 = "/TEF/lamp001/attrs/l"; // Tópico MQTT de envio da luminosidade
const char* TOPICO_PUBLISH_3 = "/TEF/lamp001/attrs/temp"; // Tópico MQTT de envio da temperatura
const char* TOPICO_PUBLISH_4 = "/TEF/lamp001/attrs/hum";  // Tópico MQTT de envio da umidade
const char* ID_MQTT = "fiware_001"; // ID MQTT
const int D4 = 2; // Pino do LED onboard

WiFiClient espClient;
PubSubClient MQTT(espClient);
char EstadoSaida = '0';

void initSerial() {
    Serial.begin(115200);
}

void initWiFi() {
    delay(10);
    Serial.println("------Conexao WI-FI------");
    Serial.print("Conectando-se na rede: ");
    Serial.println(SSID);
    Serial.println("Aguarde");
    reconectWiFi();
}

void initMQTT() {
    MQTT.setServer(BROKER_MQTT, BROKER_PORT);
    MQTT.setCallback(mqtt_callback);
}

void setup() {
    InitOutput();
    initSerial();
    initWiFi();
    initMQTT();
    dht.begin(); // Inicia o sensor DHT22
    delay(5000);
    MQTT.publish(TOPICO_PUBLISH_1, "s|on");
}

void loop() {
    VerificaConexoesWiFIEMQTT();
    EnviaEstadoOutputMQTT();
    handleLuminosity();
    handleDHT22();  // Função para lidar com o DHT22
    MQTT.loop();
}

void reconectWiFi() {
    if (WiFi.status() == WL_CONNECTED)
        return;
    WiFi.begin(SSID, PASSWORD);
    while (WiFi.status() != WL_CONNECTED) {
        delay(100);
        Serial.print(".");
    }
    Serial.println();
    Serial.println("Conectado com sucesso na rede ");
    Serial.print(SSID);
    Serial.println("IP obtido: ");
    Serial.println(WiFi.localIP());

    digitalWrite(D4, LOW);
}

void mqtt_callback(char* topic, byte* payload, unsigned int length) {
    String msg;
    for (int i = 0; i < length; i++) {
        char c = (char)payload[i];
        msg += c;
    }
    Serial.print("- Mensagem recebida: ");
    Serial.println(msg);

    String onTopic = String("car001") + "@on|";
    String offTopic = String("car001") + "@off|";

    if (msg.equals(onTopic)) {
        digitalWrite(D4, HIGH);
        EstadoSaida = '1';
    }

    if (msg.equals(offTopic)) {
        digitalWrite(D4, LOW);
        EstadoSaida = '0';
    }
}

void VerificaConexoesWiFIEMQTT() {
    if (!MQTT.connected())
        reconnectMQTT();
    reconectWiFi();
}

void EnviaEstadoOutputMQTT() {
    if (EstadoSaida == '1') {
        MQTT.publish(TOPICO_PUBLISH_1, "s|on");
        Serial.println("- Led Ligado");
    }

    if (EstadoSaida == '0') {
        MQTT.publish(TOPICO_PUBLISH_1, "s|off");
        Serial.println("- Led Desligado");
    }
    Serial.println("- Estado do LED onboard enviado ao broker!");
    delay(1000);
}

void InitOutput() {
    pinMode(D4, OUTPUT);
    digitalWrite(D4, HIGH);
    boolean toggle = false;

    for (int i = 0; i <= 10; i++) {
        toggle = !toggle;
        digitalWrite(D4, toggle);
        delay(200);
    }
}

void reconnectMQTT() {
    while (!MQTT.connected()) {
        Serial.print("* Tentando se conectar ao Broker MQTT: ");
        Serial.println(BROKER_MQTT);
        if (MQTT.connect(ID_MQTT)) {
            Serial.println("Conectado com sucesso ao broker MQTT!");
            MQTT.subscribe(TOPICO_SUBSCRIBE);
        } else {
            Serial.println("Falha ao reconectar no broker.");
            Serial.println("Haverá nova tentativa de conexão em 2s");
            delay(2000);
        }
    }
}

// Função para lidar com a luminosidade
void handleLuminosity() {
    const int potPin = 34;
    int sensorValue = analogRead(potPin);
    int luminosity = map(sensorValue, 0, 4095, 0, 100);
    String mensagem = String(luminosity);
    Serial.print("Valor da luminosidade: ");
    Serial.println(mensagem.c_str());
    MQTT.publish(TOPICO_PUBLISH_2, mensagem.c_str());
}

// Função para lidar com o sensor DHT22
// Função para lidar com o sensor DHT22
void handleDHT22() {
    float temperature = dht.readTemperature();
    float humidity = dht.readHumidity();

    if (isnan(temperature) || isnan(humidity)) {
        Serial.println("Falha ao ler do sensor DHT22");
        return;
    }

    // Envia a temperatura
    String tempString = String(temperature);
    MQTT.publish(TOPICO_PUBLISH_3, tempString.c_str());  // Envia para o tópico de temperatura
    Serial.print("Temperatura: ");
    Serial.println(tempString.c_str());

    // Envia a umidade
    String humString = String(humidity);
    MQTT.publish(TOPICO_PUBLISH_4, humString.c_str());  // Envia para o tópico de umidade
    Serial.print("Umidade: ");
    Serial.println(humString.c_str());
}
